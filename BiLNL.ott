% Input variables.
metavar ilabels, a, b, c ::=
metavar paths , h {{tex \pi}} ::=
metavar gindex ,  n , x , y, z , w ::=  % Indexes into the graph.

indexvar ivar, i , k , j ::= 

grammar

% Intuitionistic Logic:
W, X, Y, Z :: 'Iform_' ::=
  | 1    ::   :: 1  
  | X x Y ::   :: Conj
  {{tex [[X]] \times [[Y]] }}
  | X -> Y ::   :: Imp
  {{tex [[X]] \to [[Y]] }}
  | Gf A     ::   :: G
  {{tex \mathsf{G}\,[[A]] }}
  | ( X )   :: M :: Parens 
  | h( X )  :: M :: HiddenParens 
  {{ tex [[X]] }}

% Co-intuitionistic Logic:
R, S, T :: 'Cform_' ::=
  | 0   ::   :: 0
  | S + T ::   :: Disj
  {{tex [[S]] + [[T]] }}
  | S - T  ::   :: Subtrac
  {{tex [[S]] - [[T]] }}
  | H A     ::   :: H
  {{tex \mathsf{H}\,[[A]] }}
  | ( T )   :: M :: Parens 
  | h( T )  :: M :: HiddenParens 
  {{ tex [[T]] }}

% Bi-intuitionistic (Linear) Logic:
A, B, C :: 'Bform_' ::=
  | 1    ::   :: 1
  {{tex \top }}
  | 0   ::   :: 0
  {{tex \perp }}
  | A x B ::   :: Conj
  {{tex [[A]] \times [[B]] }}
  | A (x) B ::   :: Tensor
  {{tex [[A]] \otimes [[B]] }}  
  | A + B ::   :: Disj
  {{tex [[A]] + [[B]] }}
  | A (+) B ::   :: Par
  {{tex [[A]] \oplus [[B]] }}    
  | A -> B ::   :: Imp
  {{tex [[A]] \to [[B]] }}
  | A -o B ::   :: LImp
  {{tex [[A]] \limp [[B]] }}  
  | A - B  ::   :: Subtrac
  {{tex [[A]] - [[B]] }}
  | A *- B ::   :: LSubtrac
  {{tex [[A]] \colimp [[B]] }}
  | F X     ::   :: F
  {{tex \mathsf{F}\,[[X]] }}
  | J S     ::   :: J
  {{tex \mathsf{J}\,[[S]] }}  
  | ( A )   :: M :: Parens 
  | h( A )  :: M :: HiddenParens 
  {{ tex [[A]] }}

Gr {{tex G }} :: 'E_' ::=
  | .             ::    :: Empty
  {{tex \cdot }}
  | { ( x , y ) } ::   :: El
  | ( x , y )     ::   :: El2
  | Gr , Gr'      ::   :: Ext
  | Gr U Gr'      ::   :: Union
  {{tex [[Gr]] \cup [[Gr']] }}
  | ( Gr )        ::   :: Paren
  | h( Gr )       :: M :: HiddenParens 
  {{ tex [[Gr]] }}

% Intuitionistic Contexts:
H {{tex \Theta}}, P {{tex \Phi}} :: 'IG_' ::=
  | .           ::   :: Empty
  {{tex \cdot }}
  | w : X       ::   :: El
  | H , P      ::   :: Ext
  | ( H )       :: S :: Paren
  | h( H )      :: M :: HiddenParens 
  {{ tex [[H]] }}

% Co-intuitionistic Contexts:
I {{tex \Psi}}, PI {{tex \Pi}} :: 'CG_' ::=
  | .           ::   :: Empty
  {{tex \cdot }}
  | w : T       ::   :: El
  | I , PI      ::   :: Ext
  | ( I )       :: S :: Paren
  | h( I )      :: M :: HiddenParens 
  {{ tex [[I]] }}   

% Bi-intuitionistic Contexts:
G {{tex \Gamma}}, D {{tex \Delta}} :: 'BG_' ::=
  | .           ::   :: Empty
  {{tex \cdot }}
  | w : A       ::   :: El
  | G , G'      ::   :: Ext
  | ( G )       :: S :: Paren
  | h( G )      :: M :: HiddenParens 
  {{ tex [[G]] }}   
  
terminals :: '' ::=
  | []   :: :: Box  {{ tex \Box }}
  | |=   :: :: models  {{ tex \models }}
  | |-   :: :: Turnstyle  {{tex \vdash }}
  | star :: :: star   {{ tex \mathop{\star} }}
  | >>   :: :: RR   {{ tex \gg}}
  | _    :: :: Under   {{tex \Box}}
  | \    :: :: slash  {{ tex \lambda }}
  | ~>   :: :: step  {{ tex \redto }}
  | ~*>   :: :: sstep  {{ tex \redto^* }}
  | >=   :: :: succeq {{ tex \succeq }}
  | <=   :: :: preceq {{ tex \preceq }}
  | ->   :: :: rightarrow  {{ tex \rightarrow }}
  | <-   :: :: leftarrow  {{ tex \leftarrow }}
  | -<   :: :: Yleft  {{ tex \Yleft }}
  | ~*>  :: :: mstep  {{ tex \redto^* }}
  | =    :: :: Eq
  | ==   :: :: Equiv  {{ tex \equiv }}
  | =~   :: :: Cong  {{ tex \cong }}
  | bot  :: :: Bottom   {{ tex \mathop{\perp} }}
  | top  :: :: Top   {{ tex \top }}
  | *    :: :: Cut  {{ tex \bullet}}
  | ~    :: :: Tilde  {{tex \mathop{\sim} }}
  | \    :: :: Lam {{ tex \lambda }}
  | mu   :: :: Mu {{ tex \mu }}
  | !=   :: :: Neq {{tex \neq }}
  | FV   :: :: FV  {{ tex \mathsf{FV} }}
  | in   :: :: In {{ tex \in }}


 formula :: 'formula_' ::=
  | judgement               ::  :: judgement
  | formula <= formula'     :: M :: PJ
  | ( formula )             :: M :: ParenF
  | formula && formula'     ::  :: judgement_list 
  {{ tex [[formula]] \qquad [[formula']] }}
  | ( formula )             ::  :: parens
  | x Gr y                  ::  :: GraphRel
  {{tex [[x]] [[Gr]] [[y]] }}
  | x notin Gr , G , D      ::  :: wnotin
  {{tex [[x]] \not\in |[[Gr]]|,|[[G]]|,|[[D]]| }}
  | x notin Gr , H , G , D , I      ::  :: wnotin4
  {{tex [[x]] \not\in |[[Gr]]|,|[[H]]|,|[[G]]|,|[[D]]|,|[[I]]| }}
  | x notin Gr , H      ::  :: wnotin2
  {{tex [[x]] \not\in |[[Gr]]|,|[[H]]| }}
  | x notin Gr , I      ::  :: wnotin3
  {{tex [[x]] \not\in |[[Gr]]|,|[[I]]| }}    
  | ( x , y )               :: M :: Edges
  | n in Nodes ( Gr )       :: M :: Nodes1
  {{tex [[n]] \in \mathsf{Nodes}([[Gr]]) }}
  | n in Nodes ( G )        :: M :: Nodes2
  {{tex [[n]] \in \mathsf{Nodes}([[G]]) }}  

defns
  Jtyp :: '' ::=

% Intuitionistic Logic:
defn
 Gr ; H |-I w : X :: :: ILog :: 'I_' 
 {{tex [[Gr]];[[H]] \vdash_{\mathsf{I} } [[w]] : [[X]] }}
 by

 Gr, (w1,w1);H |-I w2 : Y
 ------------------------ :: rl
 Gr;H |-I w2 : Y

 w1 Gr w2 && w2 Gr w3
 Gr,(w1,w3);H |-I w : Y
 ---------------------- :: ts
 Gr;H |-I w : Y

 ------------------ :: id
 Gr;w : Y |-I w : Y

 Gr;H |-I w1 : Y
 ----------------------- :: wk
 Gr;H, w2 : X |-I w1 : Y

 Gr;H, w2 : X, w2 : X |-I w1 : Y
 ------------------------------- :: cr
 Gr;H, w2 : X |-I w1 : Y

 Gr;H1,w1 : X, w2 : Y,H2 |-I w : Z
 --------------------------------- :: ex
 Gr;H1,w2 : Y, w1 : X,H2 |-I w : Z

 w1 Gr w2
 Gr;H , w1 : X , w2 : X |-I w : Y
 -------------------------------- :: mL
 Gr;H , w1 : X |-I w : Y

 w2 Gr w1
 Gr;H |-I w2 : Y
 --------------- :: mR
 Gr;H |-I w1 : Y

 Gr;H |-I w1 : Y
 --------------------------- :: tL
 Gr;H , w2 : 1 |-I w1 : Y

 ----------------- :: tR
 Gr;H |-I w : 1

 Gr;H , w1 : X, w1 : Y |-I w2 : Z
 -------------------------------- :: pL
 Gr;H , w1 : X x Y   |-I w2 : Z

 Gr;H1 |-I w : X && Gr;H2 |-I w : Y
 ---------------------------------- :: pR
 Gr;H1,H2 |-I w : X x Y

 w1 Gr w2
 Gr;H2 |-I w2 : X && Gr;H1, w2 : Y |-I w : Z
 ------------------------------------------- :: IL
 Gr;H1 , H2, w1 : X -> Y |-I w : Z 

 w2 notin Gr, H
 Gr, (w1,w2);H, w2 : X |-I w2 : Y
 --------------------------------- :: IR
 Gr;H |-I w1 : X -> Y

 Gr;H2 |-I w2 : X && Gr;H1,w2 : X |-I w1 : Z
 -------------------------------------------- :: cut
 Gr;H1,H2 |-I w1 : Z

 Gr;H | . |-LL w : A | .
 ----------------------- :: gR
 Gr;H |-I w : Gf A

% Co-intuitionistic Logic:
defn
 Gr ; w : S |-C I :: :: CLog :: 'C_' 
 {{tex [[Gr]];[[w]]:[[S]] \vdash_{\mathsf{C} } [[I]] }}
 by

 Gr, (w1,w1);w2 : S |-C I
 ---------------------- :: rl
 Gr;w2 : S |-C I

 w1 Gr w2 && w2 Gr w3
 Gr,(w1,w3);w : S |-C I
 ---------------------- :: ts
 Gr;w : S |-C I

 ------------------ :: id
 Gr;w : S |-C w : S

 Gr;w1 : S |-C I
 ---------------------- :: wk
 Gr;w1 : S |-C w2 : T,I

 Gr;w1 : S |-C w2 : T, w2 : T, I
 ------------------------------- :: cr
 Gr;w1 : S |-C w2 : T, I

 Gr;w : R |-C I1,w1 : S, w2 : T, I2
 ---------------------------------- :: ex
 Gr;w : R |-C I1,w2 : T, w1 : S, I2

 w1 Gr w2
 Gr;w2 : S |-C I
 --------------------------- :: mL
 Gr;w1 : S |-C I

 w2 Gr w1
 Gr;w : S |-C w2 : T , w1 : T, I
 ------------------------------- :: mR
 Gr;w : S |-C w1 : T, I

 ------------------ :: fL
 Gr;w : 0 |-C I

 Gr;w1 : S |-C I
 --------------------------- :: fR
 Gr;w1 : S |-C w2 : 0, I

 Gr;w : S |-C I1 && Gr;w : T |-C I2
 ---------------------------------- :: dL
 Gr;w : S + T |-C I1,I2

 Gr;w1 : R |-C w2 : S, w2 : T, I
 ------------------------------- :: dR
 Gr;w1 : R |-C w2 : S + T, I

 w2 notin Gr, I
 Gr,(w2,w1);w2 : S |-C w2 : T, I
 ------------------------------- :: sL
 Gr;w1 : S - T |-C I

 w2 Gr w1
 Gr;w : R |-C w2 : S , I2 && Gr;w2 : T |-C I1
 -------------------------------------------- :: sR
 Gr;w : R |-C w1 : S - T, I1,I2

 Gr;w1 : S |-C w2 : T, I2 && Gr;w2 : T |-C I1
 --------------------------------------------- :: cut
 Gr;w1 : S |-C I1,I2

 Gr;. | w : A |-LL . | I
 ----------------------- :: hL
 Gr;w : H A |-C I

% Bi-intuitionistic Linear Logic:
defn
 Gr ; H | G |-LL D | I :: :: LBILog :: 'LL_' 
 {{tex [[Gr]];[[H]] \mid [[G]] \vdash_{\mathsf{LL} } [[D]] \mid [[I]] }}
 by

 % Graphs:

 Gr, (w,w);H | G |-LL D | I
 -------------------------- :: rl
 Gr;H | G |-LL D | I

 w1 Gr w2 && w2 Gr w3
 Gr,(w1,w3);H | G |-LL D | I
 --------------------------- :: ts
 Gr;H | G |-LL D | I

 % Axiom:

 --------------------------- :: id
 Gr;. | w : A |-LL w : A | .

 % Structural Rules:

 Gr; H | G |-LL D | I
 --------------------------- :: wkL
 Gr; H, w : X | G |-LL D | I

 Gr; H | G |-LL D | I
 --------------------------- :: wkR
 Gr; H | G |-LL D | w : S, I

 Gr;H, w : X, w : X | G |-LL D | I
 --------------------------------- :: ctrL
 Gr;H, w : X | G |-LL D | I

 Gr;H | G |-LL D | w : S, w : S , I
 ---------------------------------- :: ctrR
 Gr;H | G |-LL D | w : S , I

 Gr;H | G1,w1 : A,w2 : B,G2 |-LL D | I
 -------------------------------------- :: exL
 Gr;H | G1,w2 : B,w1 : A,G2 |-LL D | I

 Gr;H1,w1 : X, w2 : Y,H2 | G |-LL D | I
 -------------------------------------- :: IexL
 Gr;H1,w2 : Y, w1 : X,H2 | G |-LL D | I

 Gr;H | G |-LL D1,w1 : A, w2 : B,D2 | I
 -------------------------------------- :: exR
 Gr;H | G |-LL D1,w2 : B, w1 : A,D2 | I

 Gr;H | G |-LL D | I1,w1 : S, w2 : T,I2
 -------------------------------------- :: CexL
 Gr;H | G |-LL D | I1,w2 : T, w1 : S,I2

 % Monotonicity Rules:

 w1 Gr w2
 Gr;H | G , w1 : A , w2 : A |-LL D | I
 ------------------------------------- :: mL
 Gr;H | G , w1 : A |-LL D | I

 w2 Gr w1
 Gr;H | G |-LL w2 : A , w1 : A, D | I
 ------------------------------------ :: mR
 Gr;H | G |-LL w1 : A, D | I

 w1 Gr w2
 Gr;H , w1 : X , w2 : X | G |-LL D | I
 ------------------------------------- :: ImL
 Gr;H , w1 : X | G |-LL D | I

 w2 Gr w1
 Gr;H | G |-LL D | w2 : T , w1 : T, I
 ------------------------------------ :: CmR
 Gr;H | G |-LL D | w1 : T, I

 % Cut:

 Gr; H1 | G1 |-LL w : A, D2 | I1 && Gr; H2 | w : A,G2 |-LL D1 | I2
 ----------------------------------------------------------------- :: cut
 Gr;H1,H2 | G1,G2 |-LL D1,D2 | I1,I2

 Gr;H2 |-I w : X && Gr;H1, w : X | G |-LL D | I
 ---------------------------------------------- :: Icut
 Gr;H1,H2 | G |-LL D | I

 Gr;H | G |-LL D | I1, w : S && Gr;w : S |-C I2
 ---------------------------------------------- :: Ccut
 Gr;H | G |-LL D | I1, I2

 % Units:

 Gr;H | G |-LL D | I
 --------------------------- :: IL
 Gr;H | G, w : 1 |-LL D | I

 ------------------------ :: IR
 Gr;. | . |-LL w : 1 | .

 ----------------------- :: flL
 Gr;. | w : 0 |-LL . | .

 Gr;H | G |-LL D | I
 ------------------------- :: flR
 Gr;H | G |-LL w : 0,D | I

 % Additives from IL and coIL:
 
 Gr;H1, w : X, w : Y,H2 | G |-LL D | I
 ------------------------------------- :: cL
 Gr;H1, w : X x Y,H2 | G |-LL D | I

 Gr;H | G |-LL D | I1, w : S, w : T, I2
 -------------------------------------- :: dR
 Gr;H | G |-LL D | I1, w : S + T, I2

 % Tensor:

 Gr; H | G1, w : A, w : B, G2 |-LL D | I
 --------------------------------------- :: tL
 Gr; H | G1, w : A (x) B, G2 |-LL D | I

 Gr; H1 | G1 |-LL w : A,D1 | I1 && Gr; H2 | G2 |-LL w : B,D2 | I2
 ---------------------------------------------------------------- :: tR
 Gr; H1,H2 | G1,G2 |-LL w : A (x) B,D1,D2 | I1,I2

 % Par:

 Gr;H1 | G1, w : A |-LL D1 | I1 && Gr;H2 | G2, w : B |-LL D2 | I2
 ---------------------------------------------------------------- :: pL
 Gr;H1,H2 | G1, G2, w : A (+) B |-LL D1,D2 | I1,I2

 Gr;H | G |-LL D1, w : A, w : B, D2 | I
 -------------------------------------- :: pR
 Gr;H | G |-LL D1, w : A (+) B, D2 | I

 % Implication:

 w2 notin Gr,H,G,D,I
 Gr, (w1,w2); H | G, w2 : A |-LL w2 : B,D | I
 --------------------------------------------- :: ImpR
 Gr; H | G |-LL w1 : A -o B,D | I

 w1 Gr w2
 Gr;H1 | G1 |-LL w2 : A,D1 | I1 && Gr;H2 | G2,w2 : B |-LL D2 | I2
 ---------------------------------------------------------------- :: ImpL
 Gr;H1,H2 | G1,G2,w1 : A -o B |-LL D1,D2 | I1,I2

 w1 Gr w2
 Gr;H1 |-I w2 : X && Gr;H2,w2 : Y | G |-LL D | I
 ----------------------------------------------- :: IImpL
 Gr;H1,H2, w1 : X -> Y | G |-LL D | I

 % Co-implication:

 w2 notin Gr,H,G,D,I
 Gr, (w2,w1);H | G,w2 : A |-LL w2 : B, D | I
 ------------------------------------------- :: sL
 Gr;H | G,w1 : A *- B |-LL D | I

 w2 Gr w1
 Gr;H1 | G1 |-LL w2 : A,D1 | I1 && Gr;H2 | G2,w2 : B |-LL D2 | I2
 ---------------------------------------------------------------- :: sR
 Gr;H1,H2 | G1,G2 |-LL w1 : A *- B,D1,D2 | I1,I2

 w2 Gr w1
 Gr;H | G |-LL D | w2 : S,I1 && Gr;w2 : T |-C I2
 ---------------------------------------------------------------- :: CsR
 Gr;H | G |-LL D | w1 : S - T,I1,I2

 % Intuitionistic Functors:

 Gr;H, w : X | G |-LL D | I
 --------------------------- :: fL
 Gr;H | G,w : F X |-LL D | I 

 Gr; H |-I w : X
 -------------------------- :: fR
 Gr; H | . |-LL w : F X | .

 Gr;H | G, w : A |-LL D | I
 ----------------------------- :: gL
 Gr;H, w : Gf A | G |-LL D | I 

 % Co-intuitionistic Functors:

 Gr; w : S |-C I
 ------------------------- :: jL
 Gr;. | w : J S |-LL . | I

 Gr;H | G |-LL D | w : S,I
 --------------------------- :: jR
 Gr;H | G |-LL D,w : J S | I

 Gr;H | G |-LL D,w : A | I
 --------------------------- :: hR
 Gr;H | G |-LL D | w : H A,I

% Bi-intuitionistic Logic:
defn
 Gr ; G |-L D :: :: BILog :: '' 
 {{tex [[Gr]];[[G]] \vdash_{\mathsf{L} } [[D]] }}
 by

 % Graphs:

 Gr, (w,w); G |-L D
 ------------------ :: rl
 Gr; G |-L D

 w1 Gr w2 && w2 Gr w3
 Gr,(w1,w3);G |-L D
 -------------------- :: ts
 Gr;G |-L D

 % Axiom:

 ------------------ :: id
 Gr;w : A |-L w : A

 % Structural Rules:

 Gr; G |-L D 
 ------------------ :: wkL
 Gr; G, w : A |-L D

 Gr; G |-L D
 ------------------ :: wkR
 Gr; G |-L w : A, D

 Gr;G, w : A, w : A |-L D
 ------------------------ :: ctrL
 Gr;G, w : A |-L D

 Gr; G |-L w : A, w : A , D
 -------------------------- :: ctrR
 Gr; G |-L w : A , D

 Gr;G1,w1 : A,w2 : B,G2 |-L D
 ---------------------------- :: exL
 Gr;G1,w2 : B,w1 : A,G2 |-L D

 Gr;G |-L D1,w1 : A, w2 : B,D2
 ----------------------------- :: exR
 Gr;G |-L D1,w2 : B, w1 : A,D2

 % Monotonicity Rules:

 w1 Gr w2
 Gr;G , w1 : A , w2 : A |-L D
 --------------------------- :: mL
 Gr;G , w1 : A |-L D

 w2 Gr w1
 Gr;G |-L w2 : A , w1 : A, D
 --------------------------- :: mR
 Gr;G |-L w1 : A, D

 % Cut:

 Gr; G1 |-L w : A, D2 && Gr; w : A,G2 |-L D1
 ------------------------------------------- :: cut
 Gr; G1,G2 |-L D1,D2

 % Units:

 Gr;G |-L D
 ----------------- :: IL
 Gr;G, w : 1 |-L D

 -------------- :: IR
 Gr;. |-L w : 1

 -------------- :: flL
 Gr;w : 0 |-L .

 Gr;G |-L D
 ---------------- :: flR
 Gr;G |-L w : 0,D

 Gr; G1, w : A, w : B, G2 |-L D
 ------------------------------ :: cL
 Gr; G1, w : A x B, G2 |-L D

 Gr; G1 |-L w : A,D1 && Gr; G2 |-L w : B,D2
 ------------------------------------------ :: cR
 Gr; G1,G2 |-L w : A x B

 % Par:

 Gr;G1, w : A |-L D1 && Gr;G2, w : B |-L D2
 ------------------------------------------ :: dL
 Gr;G1, G2, w : A + B |-L D1,D2

 Gr;G |-L D1, w : A, w : B, D2
 ----------------------------- :: dR
 Gr;G |-L D1, w : A + B, D2

 % Implication:

 w2 notin Gr,G,D
 Gr, (w1,w2); G, w2 : A |-L w2 : B,D
 ----------------------------------- :: ImpR
 Gr; G |-L w1 : A -> B,D

 w1 Gr w2
 Gr;G1 |-L w2 : A,D1 && Gr;G2,w2 : B |-L D2
 ------------------------------------------ :: ImpL
 Gr;G1,G2,w1 : A -> B |-L D1,D2

 % Co-implication:

 w2 notin Gr,G,D
 Gr, (w2,w1); G,w2 : A |-L w2 : B, D
 ----------------------------------- :: sL
 Gr;G,w1 : A - B |-L D

 w2 Gr w1
 Gr;G1 |-L w2 : A,D1 && Gr;G2,w2 : B |-L D2
 ------------------------------------------- :: sR
 Gr;G1,G2 |-L w1 : A - B,D1,D2